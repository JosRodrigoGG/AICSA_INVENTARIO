CREATE OR REPLACE PROCEDURE SAF.EST_CS_SUMARIZACION_RENGLONES
(
    V_CS_COTRATOID NUMBER
) AS 
    CURSOR C_RENGLONES(P_CONTRATOID NUMBER) IS
        SELECT
            ID,
            CODIGO_RENGLON,
            INDIRECTO_PORCENTAJE,
            CANTIDAD
        FROM SAF.EST_CS_RENGLONES
        WHERE ID_CS_CONTRATOS_SERVICIO = P_CONTRATOID;
    
    CURSOR C_ACTIVIDADES(P_CODIGO_RENGLON VARCHAR2, P_CONTRATOID NUMBER) IS
        SELECT 
            NVL(SUM(B.CANTIDAD), 1) CANTIDAD,
            NVL(SUM(B.COSTO_UNITARIO), 0) COSTO_UNITARIO,
            NVL(SUM(B.COSTO_UNITARIO * B.CANTIDAD), 0) COSTO_TOTAL
        FROM SAF.EST_CS_ACTIVIDADES B
        INNER JOIN SAF.EST_CS_RENGLONES A
            ON A.ID = B.ID_EST_CS_RENGLONES
            AND A.ID_CS_CONTRATOS_SERVICIO = P_CONTRATOID
        START WITH A.CODIGO_RENGLON = P_CODIGO_RENGLON
        CONNECT BY PRIOR A.CODIGO_RENGLON = A.CODIGO_RENGLON_PADRE;

    CURSOR C_NIVEL(P_CONTRATOID NUMBER) IS
        WITH DATOS AS (
                SELECT
                    ID,
                    LEVEL AS NIVEL
                FROM SAF.EST_CS_RENGLONES
                WHERE ID_CS_CONTRATOS_SERVICIO = P_CONTRATOID
                START WITH CODIGO_RENGLON_PADRE IS NULL
                CONNECT BY PRIOR CODIGO_RENGLON = CODIGO_RENGLON_PADRE
                ORDER SIBLINGS BY CODIGO_RENGLON
            )
        SELECT
            ID,
            NIVEL
        FROM DATOS;
BEGIN
    FOR R_NIVEL IN C_NIVEL(V_CS_COTRATOID)
    LOOP
        UPDATE SAF.EST_CS_RENGLONES
            SET NIVEL = R_NIVEL.NIVEL
        WHERE ID = R_NIVEL.ID;
    END LOOP;

    FOR R_RENGLONES IN C_RENGLONES(V_CS_COTRATOID)
    LOOP
        FOR R_ACTIVIDADES IN C_ACTIVIDADES(R_RENGLONES.CODIGO_RENGLON, V_CS_COTRATOID)
        LOOP
            UPDATE SAF.EST_CS_RENGLONES SET
                COSTO_UNITARIO = R_ACTIVIDADES.COSTO_TOTAL / R_RENGLONES.CANTIDAD,
                COSTO_TOTAL = R_ACTIVIDADES.COSTO_TOTAL,
                INDIRECTO = (R_ACTIVIDADES.COSTO_TOTAL) * (R_RENGLONES.INDIRECTO_PORCENTAJE / 100),
                PRECIO_UNITARIO = (((R_ACTIVIDADES.COSTO_TOTAL) * (R_RENGLONES.INDIRECTO_PORCENTAJE / 100)) + R_ACTIVIDADES.COSTO_TOTAL) / R_RENGLONES.CANTIDAD,
                PRECIO_TOTAL = (((R_ACTIVIDADES.COSTO_TOTAL) * (R_RENGLONES.INDIRECTO_PORCENTAJE / 100)) + R_ACTIVIDADES.COSTO_TOTAL)
            WHERE ID = R_RENGLONES.ID;
        END LOOP;
    END LOOP;
END;
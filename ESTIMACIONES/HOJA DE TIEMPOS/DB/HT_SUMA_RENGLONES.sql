CREATE OR REPLACE PROCEDURE SAF.HT_SUMA_RENGLONES
(
    V_HOJA_TIEMPO NUMBER,
    V_DIAS_CALCULAR NUMBER
) AS
    CURSOR C_FECHA_EVALUAR(P_DIAS NUMBER, P_ID NUMBER) IS
        SELECT
            (TRUNC(FECHA_INICIO) + P_DIAS - 1) FECHA
        FROM SAF.HT_HOJA_DE_TIEMPOS
        WHERE ID = P_ID;

    CURSOR C_RENGLONES(P_ID NUMBER, P_FECHA DATE) IS
        SELECT
            ID,
            CODIGO_RENGLON,
            CODIGO_RENGLON_PADRE,
            CANTIDAD_HORAS
        FROM SAF.HT_RENGLONES_SERVICOS
        WHERE ID_HT_HOJA_DE_TIEMPOS = P_ID
          AND TRUNC(FECHA) = TRUNC(P_FECHA)
          AND TIPO_REGISTRO != 'TA';

    CURSOR C_SUMA_ACTIVIDADES(P_ID_HT NUMBER, P_ID_RENGLON NUMBER) IS
        SELECT
            SUM(CANTIDAD_HORAS) HORAS
        FROM SAF.HT_ACTIVIDADES_SERVICOS
        WHERE ID_HT_HOJA_DE_TIEMPOS = P_ID_HT
          AND ID_HT_RENGLONES_SERVICOS = P_ID_RENGLON
          AND TIPO_REGISTRO != 'TA';

    CURSOR C_RENGLONES_NIVEL(P_ID_HT NUMBER, P_FECHA DATE) IS
        WITH RENGLONES AS (
            SELECT
                ID,
                CODIGO_RENGLON,
                CODIGO_RENGLON_PADRE,
                CANTIDAD_HORAS
            FROM SAF.HT_RENGLONES_SERVICOS
            WHERE ID_HT_HOJA_DE_TIEMPOS = P_ID_HT
            AND TRUNC(FECHA) = TRUNC(P_FECHA)
            AND TIPO_REGISTRO != 'TA'
        ), RENGLONES_NIVEL AS (
            SELECT
                LEVEL AS NIVEL,
                A.*
            FROM RENGLONES A
            START WITH A.CODIGO_RENGLON_PADRE IS NULL
            CONNECT BY PRIOR A.CODIGO_RENGLON = A.CODIGO_RENGLON_PADRE
        ) SELECT * FROM RENGLONES_NIVEL ORDER BY NIVEL DESC;

    CURSOR C_RENGLONES_BUSCAR(P_ID_HT NUMBER, P_FECHA DATE, P_CODIGO VARCHAR2) IS
        SELECT
            SUM(CANTIDAD_HORAS)
        FROM SAF.HT_RENGLONES_SERVICOS
        WHERE ID_HT_HOJA_DE_TIEMPOS = P_ID_HT
        AND TRUNC(FECHA) = TRUNC(P_FECHA)
        AND CODIGO_RENGLON_PADRE = P_CODIGO
        AND TIPO_REGISTRO != 'TA';

    CURSOR C_TAREAS(P_ID_HT NUMBER, P_FECHA DATE) IS
        SELECT
            NVL(SUM(CANTIDAD_HORAS), 0)
        FROM SAF.HT_TAREAS_ADMINISTRATIVAS
        WHERE ID_HT_HOJA_DE_TIEMPOS = P_ID_HT
        AND TRUNC(FECHA) = TRUNC(P_FECHA);

    V_FECHA DATE;
    V_SUMA NUMBER;
BEGIN
    OPEN C_FECHA_EVALUAR(V_DIAS_CALCULAR, V_HOJA_TIEMPO);
        FETCH C_FECHA_EVALUAR INTO V_FECHA;
    CLOSE C_FECHA_EVALUAR;

    FOR R_RENGLONES IN C_RENGLONES(V_HOJA_TIEMPO, V_FECHA)
    LOOP
        FOR R_SUMA_ACTIVIDADES IN C_SUMA_ACTIVIDADES(V_HOJA_TIEMPO, R_RENGLONES.ID)
        LOOP
            UPDATE SAF.HT_RENGLONES_SERVICOS
                SET CANTIDAD_HORAS = NVL(R_SUMA_ACTIVIDADES.HORAS, R_RENGLONES.CANTIDAD_HORAS)
            WHERE ID = R_RENGLONES.ID;
        END LOOP;
    END LOOP;

    FOR R_RENGLONES_NIVEL IN C_RENGLONES_NIVEL(V_HOJA_TIEMPO, V_FECHA)
    LOOP
        OPEN C_RENGLONES_BUSCAR(V_HOJA_TIEMPO, V_FECHA, R_RENGLONES_NIVEL.CODIGO_RENGLON);
            FETCH C_RENGLONES_BUSCAR INTO V_SUMA;
        CLOSE C_RENGLONES_BUSCAR;

        IF V_SUMA IS NOT NULL THEN
            UPDATE SAF.HT_RENGLONES_SERVICOS
                SET CANTIDAD_HORAS = V_SUMA
            WHERE ID = R_RENGLONES_NIVEL.ID;
        END IF;
    END LOOP;

    OPEN C_TAREAS(V_HOJA_TIEMPO, V_FECHA);
        FETCH C_TAREAS INTO V_SUMA;
    CLOSE C_TAREAS;

    UPDATE SAF.HT_RENGLONES_SERVICOS
        SET CANTIDAD_HORAS = V_SUMA
    WHERE ID_HT_HOJA_DE_TIEMPOS = V_HOJA_TIEMPO
    AND TRUNC(FECHA) = TRUNC(V_FECHA)
    AND TIPO_REGISTRO = 'TA';
END;